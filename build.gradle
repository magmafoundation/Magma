buildscript {
    repositories {
        maven {
            url = 'https://files.minecraftforge.net/maven'
        }
        jcenter()
        mavenCentral()
        maven {
            name = 'PaperMC'
            url = 'https://papermc.io/repo/repository/maven-public/'
        }
        maven {
            name = 'SpongePowered'
            url = 'https://dist.creeper.host/Sponge/maven/'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            name = "bungeecord-repo"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
        classpath 'com.destroystokyo.paper:paper-api:1.14.4-R0.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "org.ajoberstar.grgit:grgit-gradle:4.0.0"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "org.ajoberstar.grgit"

version = gitVersion()

def gitVersion() {
    def raw = grgit.describe(longDescr: true, tags: true)
    def desc = (raw == null ? 'unknown-offset-hash' : raw).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name
    project.ext.SPEC_VERSION = tag
    return "${tag}.${offset}${t -> if (branch != 'master') t << '-' + branch}"
}

group = modGroup
archivesBaseName = modBaseName

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20190719-1.14.3'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
//            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                darkforgereborn {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                darkforgereborn {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'darkforgereborn', '--all', '--output', file('src/generated/resources/')

            mods {
                darkforgereborn {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft("net.minecraftforge:forge:1.14.4-28.1.93")
    compile("org.spongepowered:mixin:0.8-SNAPSHOT")
    shadow('org.fusesource.jansi:jansi:1.18')
    shadow("com.destroystokyo.paper:paper-api:1.14.4-R0.1-SNAPSHOT") {
        exclude(module: "asm")
        exclude(module: "asm-util")
        exclude(module: "asm-commons")
    }
}

mixin {
    add sourceSets.main, "mixins.magma.bridge.refmap.json"
    add sourceSets.main, "mixins.magma.core.refmap.json"

    defaultObfuscationEnv "notch"
}

shadowJar {
    setArchivesBaseName("Magma")
    configurations = [project.configurations.shadow]
    manifest {
        attributes(
                "MixinConnector": "org.magmafoundation.magma.launcher.Launcher",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": "0",
                "MixinConfigs": "mixins.magma.bridge.json,mixins.magma.core.json"
        )
    }
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn(shadowJar)

apply from: "gradle/scripts/dependencies.gradle"


